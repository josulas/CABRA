#include <Arduino.h>

// defines
#define SAMPLERATE 8000 // Hz
#define NSAMPLESPERBUFFER 128
#define SERIALBAUD 960000
#define READPIN 4
#define INTERRUPTPIN 2

// Task related variables
TaskHandle_t sendTask;
hw_timer_t *samplerTimer = NULL;
volatile bool sampling_done = false;

// ADC related variables
volatile bool currentBufferIsA = true;
volatile uint16_t adcBufferA[NSAMPLESPERBUFFER];
volatile uint16_t adcBufferB[NSAMPLESPERBUFFER];
volatile uint adcRead = 0;
volatile uint16_t adcBufferIdx = 0;
volatile uint16_t readVal = 0;
volatile long n_samples = 0;
char startSamplingFlag = 0;


void sendBuffer(){
  // Choose the right bufffer, which is not in use by the ADC
  if (!currentBufferIsA){
    // Send the buffer
    Serial.write((uint8_t *) adcBufferA, NSAMPLESPERBUFFER * 2);
  }
  else{
    // Send the buffer
    Serial.write((uint8_t *) adcBufferB, NSAMPLESPERBUFFER * 2);
  }
}

void readADC(){
  // Read ADC value and write it to the SD card
  readVal = analogRead(READPIN);
  if (currentBufferIsA){
    adcBufferA[adcBufferIdx++] = readVal;
  }
  else{
    adcBufferB[adcBufferIdx++] = readVal;
  }
  if (adcBufferIdx == NSAMPLESPERBUFFER){
    currentBufferIsA = !currentBufferIsA;
    adcBufferIdx = 0;
    vTaskResume(sendTask);
  }
}

void IRAM_ATTR resumeSampling(){
  if (!sampling_done && !timerAlarmEnabled(samplerTimer)){
    timerRestart(samplerTimer);
    timerAlarmEnable(samplerTimer);
  }
}

void IRAM_ATTR samplerTimerISER(){
  if (adcRead >= n_samples){
    sampling_done = true;
  }
  else {
    adcRead++;
    readADC();
  }
}

void sendDataTask(void *pvParameters){
  for (;;){
    vTaskSuspend(NULL);
    sendBuffer();
  }
}

void setup(){
  // Variable setup
  adcRead = 0;
  sampling_done = true;
  adcBufferIdx = 0;
  currentBufferIsA = true;

  // Pin configuration
  pinMode(READPIN, INPUT);               // ADC pin
  pinMode(INTERRUPTPIN, INPUT_PULLUP);   // Interrupt pin

  // Serial initialization
  Serial.setTxBufferSize(2 * NSAMPLESPERBUFFER);
  Serial.begin(SERIALBAUD);
  if (!Serial) {
    return;
  }
  
  // Timer configuration
  samplerTimer = timerBegin(0, 80, true);    // 80 MHz / 800 = 1 MHz
  timerAttachInterrupt(samplerTimer, &samplerTimerISER, true);
  timerAlarmWrite(samplerTimer, (1000000 / SAMPLERATE), true); 

  // ADC configuration
  // analogSetAttenuation(ADC_0db);

  // Create a task that will send the data to the Raspberry Pi
  xTaskCreatePinnedToCore(
    sendDataTask,   /* Function to implement the task */
    "sendDataTask", /* Name of the task */
    10000,  /* Stack size in words */
    NULL,   /* Task input parameter */
    0,      /* Priority of the task */
    &sendTask,   /* Task handle. */
    0);     /* Core where the task should run */

  attachInterrupt(INTERRUPTPIN, resumeSampling, RISING);
}


void loop(){
  while(!Serial.available()) {}
  n_samples = Serial.parseInt();
  // Attach the interrupt to the pin
  sampling_done = false;
  // Wait for the sampling and sending to finish
  while (!sampling_done) {}
  timerAlarmDisable(samplerTimer);
  // Send the last buffer, if it is not empty
  if (adcBufferIdx > 0){
    currentBufferIsA = !currentBufferIsA;
    sendBuffer();
  }
  adcRead = 0;
  adcBufferIdx = 0;
  currentBufferIsA = true;
  n_samples = 0;
  ESP.restart();
}